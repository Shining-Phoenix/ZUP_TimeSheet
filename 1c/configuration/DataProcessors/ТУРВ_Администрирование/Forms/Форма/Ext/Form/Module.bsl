#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Объект.Логин = "user@mail.ru";
	Объект.Пароль = "111111";
	                      
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыИмяТаблицыФормы

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьПользователя(Команда)
	
	СоздатьПользователяНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьСотрудниковПоПользователю(Команда)
	
	СоздатьСотрудниковПоПользователюНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьФото(Команда)
	
	ОбновитьФотоНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьКадровуюИсторию(Команда)
	СоздатьКадровуюИсториюНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОбмен(Команда)
	ВыполнитьОбменНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СоздатьРасчетныеЛистки(Команда)
	СоздатьРасчетныеЛисткиНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СоздатьИсториюИзмененияГрафиков(Команда)
	СоздатьИсториюИзмененияГрафиковНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ВыполнитьОбменНаСервере()
	
	ТУРВ_ОбменДаннымиСервер.ВыполнитьОбмен();
	
КонецПроцедуры

&НаСервере
Процедура СоздатьПользователяНаСервере()
	
	НастройкиСервиса = ТУРВ_РаботаСФункциямиКлиентСервер.Результат(ТУРВ_ОбменДаннымиПовтИсп.НастройкиСервиса());
	Токен = ТУРВ_РаботаСФункциямиКлиентСервер.Результат(ТУРВ_ОбменДаннымиСервер.Логин(НастройкиСервиса));
	Результат = ТУРВ_ОбменДаннымиСервер.СоздатьПользователя(НастройкиСервиса, Токен, Объект.Пользователь, Объект.Логин, Объект.Пароль);
	
	Если ТУРВ_РаботаСФункциямиКлиентСервер.Ошибка(Результат) Тогда
		ТУРВ_РаботаСФункциямиКлиентСервер.СообщитьОбОшибках(Результат);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СоздатьСотрудниковПоПользователюНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Сотрудники.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.Сотрудники КАК Сотрудники
	               |ГДЕ
	               |	Сотрудники.ФизическоеЛицо = &ФизическоеЛицо";
	
	Запрос.УстановитьПараметр("ФизическоеЛицо", Объект.Пользователь);
	
	НастройкиСервиса = ТУРВ_РаботаСФункциямиКлиентСервер.Результат(ТУРВ_ОбменДаннымиПовтИсп.НастройкиСервиса());
	Токен = ТУРВ_РаботаСФункциямиКлиентСервер.Результат(ТУРВ_ОбменДаннымиСервер.Логин(НастройкиСервиса));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Результат = ТУРВ_ОбменДаннымиСервер.СоздатьСотрудника(НастройкиСервиса, Токен, "", Выборка.Ссылка);
		
		Если ТУРВ_РаботаСФункциямиКлиентСервер.Ошибка(Результат) Тогда
			ТУРВ_РаботаСФункциямиКлиентСервер.СообщитьОбОшибках(Результат);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция  ОбновитьФотоНаСервере()
	
	НастройкиСервиса = ТУРВ_РаботаСФункциямиКлиентСервер.Результат(ТУРВ_ОбменДаннымиПовтИсп.НастройкиСервиса());
	Токен = ТУРВ_РаботаСФункциямиКлиентСервер.Результат(ТУРВ_ОбменДаннымиСервер.Логин(НастройкиСервиса));
			
	Результат = ТУРВ_ОбменДаннымиСервер.ОбновитьФотоПользователя(НастройкиСервиса, Токен, "", Объект.Пользователь);
	
	Если ТУРВ_РаботаСФункциямиКлиентСервер.Ошибка(Результат) Тогда
		ТУРВ_РаботаСФункциямиКлиентСервер.СообщитьОбОшибках(Результат);
	КонецЕсли;

КонецФункции

&НаСервере
Процедура СоздатьКадровуюИсториюНаСервере()
	
	НастройкиСервиса = ТУРВ_РаботаСФункциямиКлиентСервер.Результат(ТУРВ_ОбменДаннымиПовтИсп.НастройкиСервиса());
	Токен = ТУРВ_РаботаСФункциямиКлиентСервер.Результат(ТУРВ_ОбменДаннымиСервер.Логин(НастройкиСервиса));

	ЗапросКадровойИстории = Новый Запрос;
	ЗапросКадровойИстории.Текст = 	"ВЫБРАТЬ
									|	КадроваяИсторияСотрудниковИнтервальный.ДатаНачала КАК ДатаНачала,
									|	КадроваяИсторияСотрудниковИнтервальный.Должность КАК Должность,
									|	КадроваяИсторияСотрудниковИнтервальный.Подразделение КАК Подразделение,
									|	КадроваяИсторияСотрудниковИнтервальный.Сотрудник КАК Сотрудник
									|ИЗ
									|	РегистрСведений.КадроваяИсторияСотрудниковИнтервальный КАК КадроваяИсторияСотрудниковИнтервальный
									|ГДЕ
									|	КадроваяИсторияСотрудниковИнтервальный.ФизическоеЛицо = &ФизическоеЛицо
									|ИТОГИ ПО
									|	Сотрудник";
	ЗапросКадровойИстории.УстановитьПараметр("ФизическоеЛицо", Объект.Пользователь);
	
	Выборка = ЗапросКадровойИстории.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока Выборка.Следующий() Цикл
		СтруктураРабочихМест = Новый Структура;
		МассивРабочихМест = Новый Массив;
		
		СотрудникСсылка = Выборка.Сотрудник;
		
		ВыборкаКадровойИстории = Выборка.Выбрать();
		Пока ВыборкаКадровойИстории.Следующий() Цикл
			СтруктураРабочегоМеста = Новый Структура;
			СтруктураРабочегоМеста.Вставить("position_pk",    ВыборкаКадровойИстории.Должность);
			СтруктураРабочегоМеста.Вставить("subdivision_pk", ВыборкаКадровойИстории.Подразделение);
			СтруктураРабочегоМеста.Вставить("date_from",      ВыборкаКадровойИстории.ДатаНачала);
			МассивРабочихМест.Добавить(СтруктураРабочегоМеста);
		КонецЦикла;
		
		Результат = ТУРВ_ОбменДаннымиСервер.СоздатьОбновитьКадровойИсториюСотрудника("", НастройкиСервиса, Токен, СотрудникСсылка, МассивРабочихМест);
		
		Если ТУРВ_РаботаСФункциямиКлиентСервер.Ошибка(Результат) Тогда
			ТУРВ_РаботаСФункциямиКлиентСервер.СообщитьОбОшибках(Результат);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СоздатьРасчетныеЛисткиНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	НачисленияУдержанияПоСотрудникам.ФизическоеЛицо КАК ФизическоеЛицо,
	               |	НачисленияУдержанияПоСотрудникам.Сотрудник КАК Сотрудник,
	               |	НачисленияУдержанияПоСотрудникам.Организация КАК Организация,
	               |	НачисленияУдержанияПоСотрудникам.ПериодДействия КАК ПериодДействия
	               |ИЗ
	               |	РегистрНакопления.НачисленияУдержанияПоСотрудникам КАК НачисленияУдержанияПоСотрудникам
	               |ГДЕ
	               |	НачисленияУдержанияПоСотрудникам.ФизическоеЛицо = &ФизическоеЛицо
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Организация,
	               |	Сотрудник,
	               |	ФизическоеЛицо,
	               |	ПериодДействия";

    Запрос.УстановитьПараметр("ФизическоеЛицо", Объект.Пользователь);
	
		
	НастройкиСервиса = ТУРВ_РаботаСФункциямиКлиентСервер.Результат(ТУРВ_ОбменДаннымиПовтИсп.НастройкиСервиса());
	Токен = ТУРВ_РаботаСФункциямиКлиентСервер.Результат(ТУРВ_ОбменДаннымиСервер.Логин(НастройкиСервиса));
		
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		Результат = ТУРВ_ОбменДаннымиСервер.ДобавитьОбновитьРасчетныйЛист(НастройкиСервиса, Токен, "", Выборка.ПериодДействия, Выборка.Сотрудник, Выборка.ФизическоеЛицо); 
		
		Если ТУРВ_РаботаСФункциямиКлиентСервер.Ошибка(Результат) Тогда
			ТУРВ_РаботаСФункциямиКлиентСервер.СообщитьОбОшибках(Результат);
		КонецЕсли;
	КонецЦикла;	
КонецПроцедуры

&НаСервере
Процедура СоздатьИсториюИзмененияГрафиковНаСервере()	
	
	Запрос = Новый Запрос;
	Запрос.Текст ="
					|ВЫБРАТЬ РАЗЛИЧНЫЕ
					|	МИНИМУМ(НАЧАЛОПЕРИОДА(ГрафикРаботыСотрудников.Период, ДЕНЬ)) КАК Период,
					|	ГрафикРаботыСотрудников.Сотрудник КАК Сотрудник
					|ИЗ
					|	РегистрСведений.ГрафикРаботыСотрудников КАК ГрафикРаботыСотрудников
					|ГДЕ
					|	ГрафикРаботыСотрудников.Сотрудник.ФизическоеЛицо = &ФизическоеЛицо
					|
					|СГРУППИРОВАТЬ ПО
					|	ГрафикРаботыСотрудников.Сотрудник
					|";
	Запрос.УстановитьПараметр("ФизическоеЛицо", Объект.Пользователь);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда 
		
		НастройкиСервиса = ТУРВ_РаботаСФункциямиКлиентСервер.Результат(ТУРВ_ОбменДаннымиПовтИсп.НастройкиСервиса());
		Токен = ТУРВ_РаботаСФункциямиКлиентСервер.Результат(ТУРВ_ОбменДаннымиСервер.Логин(НастройкиСервиса));
		
		ТипОбъекта	= ТУРВ_ОбменДаннымиПовтИсп.ТипыОбъектов().РСНЗ;
		ИмяОбъекта = "ГрафикРаботыСотрудников";
		
		Структура = Новый Структура;
		Структура.Вставить("Сотрудник", Выборка.Сотрудник);
		Структура.Вставить("Период", 	Выборка.Период);
		
		ДанныеJSON = ТУРВ_РаботаСJSONСервер.ЗаписьJSON(Структура);	
		
		ХешированиеДанных  = Новый ХешированиеДанных (ХешФункция.CRC32);
		ХешированиеДанных.Добавить(ДанныеJSON);
		ХешСумма = ХешированиеДанных.ХешСумма;
		
		РегистрыСведений.ТУРВ_ОчередьОбменаПроизвольнымиДанными.ЗарегистрироватьДобавлениеОбъекта(ТипОбъекта, ИмяОбъекта, ХешСумма, ДанныеJSON);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
